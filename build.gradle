plugins {
    id "org.jetbrains.intellij" version "0.4.21"
    id "com.adarshr.test-logger" version "1.7.0"
    id "idea"
    id "java"
    id "jacoco"
}

downloadRobotServerPlugin.version = remoteRobotVersion

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repository.jboss.org'
    }
    maven {
        url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    def versions = ideaVersion.substring(3).split("\\.")
    if (versions[0] == "2021" || versions[0] == "2020" || (versions[0] == "2019" && versions[1] != "1")) {
        plugins 'java', 'terminal'
    } else {
        plugins 'terminal'
    }
    pluginName 'Runtime Server Protocol Connector by Red Hat'
    updateSinceUntilBuild false
}

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'io.fabric8'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

dependencies {
    compile 'com.redhat.devtools.intellij:intellij-common:1.4.0-SNAPSHOT'
    compile 'com.squareup.okhttp3:okhttp:3.12.12'
    compile group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j.jsonrpc', version: '0.9.0'
    compile 'org.jboss.tools.rsp.api.bundles:org.jboss.tools.rsp.api:0.22.9.Final@jar'
    compile 'com.redhat.devtools.intellij:intellij-common-ui-test-library:0.0.2'
}

runIdeForUiTests {
    systemProperty "robot-server.port", System.getProperty("robot-server.port")
}

group 'com.redhat.devtools.intellij'
version projectVersion // Plugin version
